plugins {
    id 'org.springframework.boot' version '2.1.6.RELEASE'
    id 'net.ltgt.apt' version '0.21'
    id 'net.ltgt.apt-idea' version '0.21'
    id 'org.jetbrains.gradle.plugin.idea-ext' version '0.5'
    id 'java'
}

apply plugin: 'io.spring.dependency-management'
apply plugin: 'net.ltgt.apt-idea'

group = 'com.jdriven'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '1.8'

configurations {
    developmentOnly
    runtimeClasspath {
        extendsFrom developmentOnly
    }
    compileOnly {
        extendsFrom annotationProcessor
    }
    springinstrument
}

repositories {
    mavenCentral()
}

ext {
    set('springCloudVersion', "Greenwich.SR2")
    set('mapstructVersion', '1.3.0.Final')
    set('jacksonVersion', '2.9.8')
}

dependencies {
    compileOnly 'org.projectlombok:lombok'

    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-aop'
    implementation 'org.springframework.cloud:spring-cloud-starter-openfeign'
    implementation 'org.springframework.kafka:spring-kafka'
    implementation "org.mapstruct:mapstruct-jdk8:${mapstructVersion}"

    implementation "com.fasterxml.jackson.core:jackson-core:${jacksonVersion}"
    implementation "com.fasterxml.jackson.datatype:jackson-datatype-jdk8:${jacksonVersion}"
    implementation "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:${jacksonVersion}"
    implementation "com.fasterxml.jackson.module:jackson-module-parameter-names:${jacksonVersion}"

    runtime 'com.h2database:h2:1.4.199'

    developmentOnly 'org.springframework.boot:spring-boot-devtools'

    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
    annotationProcessor "org.mapstruct:mapstruct-processor:${mapstructVersion}"
    annotationProcessor 'org.projectlombok:lombok'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'

    runtime('org.springframework:spring-instrument:5.+')
    springinstrument "org.springframework:spring-instrument:5.+"
    runtime configurations.springinstrument.dependencies
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

bootRun {
    jvmArgs "-javaagent:${configurations.springinstrument.asPath}"
}

test.doFirst {
    jvmArgs "-javaagent:${configurations.springinstrument.asPath}"
}

idea {
    module {
        inheritOutputDirs = false
        outputDir = compileJava.destinationDir
        testOutputDir = compileTestJava.destinationDir
    }
}
